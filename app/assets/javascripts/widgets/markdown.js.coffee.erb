<%

def render_html (tpl)
  dir = Rails.root.join("app", "views")
  view = ActionView::Base.new(dir, :user => @user)
  view.extend(ApplicationHelper)
  view.extend(IconHelper)
  view.render(partial: tpl, layout: nil)
end

font_awesome_selector = render_html('admin/shared/fontawesome').gsub('"', '\"').gsub("\n", '')

use_aws = Rails.env.production? || Rails.env.staging? || ENV['FORCE_AWS']

ignored = ["'cmdCode'"]
ignored << "'cmdImage'" if use_aws
%>

id = 0

wrap = (wrapper) -> (e) ->
  # Give ![] surround the selection and prepend the image link
  chunk = undefined
  cursor = undefined
  selected = e.getSelection()
  content = e.getContent()
  if selected.length == 0
    # Give extra word
    chunk = e.__localize('enter text here')
  else
    chunk = selected.text

  e.replaceSelection wrapper + '\n' + chunk + '\n' + wrapper
  cursor = selected.start + 1 + wrapper.length
  # Set the cursor
  e.setSelection cursor, cursor + chunk.length

insertImage = (link, e) ->
  # Give ![] surround the selection and prepend the image link
  chunk = undefined
  cursor = undefined
  selected = e.getSelection()
  content = e.getContent()
  if selected.length == 0
    # Give extra word
    chunk = e.__localize('enter image description here')
  else
    chunk = selected.text

  sanitizedLink = $('<div>' + link + '</div>').text()
  # transform selection and set the cursor into chunked text
  e.replaceSelection '![' + chunk + '](' + sanitizedLink + ' "' + e.__localize('enter image title here') + '")'
  cursor = selected.start + 2
  # Set the next tab
  e.setNextTab e.__localize('enter image title here')
  # Set the cursor
  e.setSelection cursor, cursor + chunk.length

insertIcon = (icon, e) ->
  # Give ![] surround the selection and prepend the image link
  cursor = undefined
  selected = e.getSelection()
  content = e.getContent()

  # transform selection and set the cursor into chunked text
  e.replaceSelection "%(#{icon}) "
  cursor = selected.start + icon.mength + 4
  # Set the next tab
  e.setNextTab ''
  # Set the cursor
  e.setSelection cursor, cursor

iconChoice = (editor, e) ->
  markup = "<%= font_awesome_selector %>"

  $modal = $(markup)
  $(document.body).append($modal)

  $modal.find('.btn').on 'click', ->
    $modal.remove()

  $modal.find('.icon').on 'click', ->
    $li = $(this)

    insertIcon($li.data('value'), e)
    $modal.remove()

insertButton = (e) ->
  # Give [] surround the selection and prepend the link
  chunk = undefined
  cursor = undefined
  selected = e.getSelection()
  content = e.getContent()
  link = undefined
  if selected.length == 0
    # Give extra word
    chunk = e.__localize('enter link description here')
  else
    chunk = selected.text
  link = prompt(e.__localize('Insert Hyperlink'), 'http://')
  urlRegex = new RegExp('^((http|https)://|(mailto:)|(//))[a-z0-9]', 'i')
  if link != null and link != '' and link != 'http://' and urlRegex.test(link)
    sanitizedLink = $('<div>' + link + '</div>').text()
    # transform selection and set the cursor into chunked text
    e.replaceSelection '(' + chunk + '=>' + sanitizedLink + ')'
    cursor = selected.start + 1
    # Set the cursor
    e.setSelection cursor, cursor + chunk.length
  return

uploadFile = (editor, e) ->
  uploader  = $('.direct-upload').first().clone()
  uploader.insertAfter($('.direct-upload').first())
  redirect_url = document.location.protocol + '//' + document.location.host + '/result.html'
  progress = editor.find('.progress')

  $file_field = uploader.find('input[type="file"]')
  $file_field.attr('id', "markdown-uploader-#{id}")

  is_ie = $('html').hasClass('ie9') or $('html').hasClass('lt-ie9')

  $(uploader).fileupload
    url:          uploader.attr('action')
    type:         'POST'
    autoUpload:   true
    sequentialUploads: true
    crossDomain: true
    redirect: redirect_url if is_ie
    redirectParamName: 'success_action_redirect' if is_ie
    forceIframeTransport: is_ie

    # This is really important as s3 gives us back the url of the file in a XML document
    # dataType:     'xml',
    dataType:     'text',

    add: (event, data) ->
      signed_data =
        doc:
          title: data.files[0].name

      signed_data['success_action_redirect'] = redirect_url if is_ie

      $.ajax
        url:      '/admin/signed_urls'
        type:     'GET'
        dataType: 'json'
        async:    false

        # send the file name to the server so it can generate the key param
        data: signed_data

        success:  (data) ->
          # Now that we have our data, we update the form so it contains all
          # the needed data to sign the request
          uploader.find('input[name=key]').val(String(data.key))
          uploader.find('input[name=policy]').val(String(data.policy))
          uploader.find('input[name=signature]').val(String(data.signature))

      prevUpload.abort() if prevUpload
      prevUpload = data.submit()

    send: (e, data) ->
      progress.fadeIn(100)

    progress: (e, data) ->
      # This is what makes everything really cool, thanks to that callback
      # you can now update the progress bar based on the upload progress
      percent = Math.round((e.loaded / e.total) * 100)
      progress.find('.progress-bar').css('width', percent + '%')

    fail: (e, data) ->
      console.log(e, data)

    success: (data) ->
      if data.indexOf('<?xml') is -1
        url = uploader.attr('action') + '/' + data.split('&')[1].split('=')[1]
        insertImage(url, e)
      else
        # Here we get the file url on s3 in an xml doc
        url = $(data).find('Location').text()
        insertImage(url, e)

    done: (event, data) ->
      progress.fadeOut 300, -> progress.find('.progress-bar').css('width', 0)

  $file_field.trigger('click')

  id++

widgets.define 'markdown', (el) ->
  $md_editor = null
  $(el).markdown({
    hiddenButtons: [<%= ignored.join(', ') %>]
    reorderButtonGroups: [
      'groupFont'
      'groupParagraph'
      'groupLink'
      'groupMisc'
      'groupHelp'
      'groupUtil'
    ]
    additionalButtons: [
      {
        name: "groupLink"
        data: [
          <% if use_aws %>
          {
            name: "cmdUploadImage"
            toggle: false
            title: "Image"
            icon: "fa fa-picture-o"
            callback: (e) ->
              uploadFile($md_editor, e)
          }
          <% end %>
          {
            name: "cmdIcon"
            toggle: false
            title: "Icône"
            icon: "fa fa-magic"
            callback: (e) ->
              iconChoice($md_editor, e)
          }
          {
            name: "cmdButton"
            toggle: false
            title: "Boutons"
            icon: "fa fa-hand-pointer-o"

            callback: insertButton
          }
        ]
      }
      {
        name: "groupParagraph"
        data: [
          {
            name: "cmdTextRight"
            toggle: false
            title: "Aligner à droite"
            icon: "fa fa-align-right"
            callback: wrap('-->>')
          }
          {
            name: "cmdTextCenter"
            toggle: false
            title: "Centré"
            icon: "fa fa-align-center"
            callback: wrap('-><-')
          }
          {
            name: "cmdTextLeft"
            toggle: false
            title: "Aligner à gauche"
            icon: "fa fa-align-left"
            callback: wrap('<<--')
          }
        ]
      }
      {
        name: "groupFont"
        data: [
          {
            name: "cmdLettrine"
            toggle: false
            title: "Lettrine"
            icon: "fa fa-text-height"
            callback: (e) ->
              selected = e.getSelection()

              e.replaceSelection("^^^#{selected.text}^^^")
              e.setSelection(selected.start + 3,selected.start + 3 + selected.length)
          }
        ]
      }
      {
        name: "groupMisc"
        data: [
          {
            name: "cmdWell"
            toggle: false
            title: "Encart fond gris"
            icon: "fa fa-square"
            callback: (e) ->
              selected = e.getSelection()

              e.replaceSelection("%%\n#{selected.text}\n%%")
              e.setSelection(selected.start + 3,selected.start + 3 + selected.length)
          }
          {
            name: "cmdWellNarrow"
            toggle: false
            title: "Encart centré"
            icon: "fa fa-square-o"
            callback: (e) ->
              selected = e.getSelection()

              e.replaceSelection("%%%\n#{selected.text}\n%%%")
              e.setSelection(selected.start + 4,selected.start + 4 + selected.length)
          }
        ]
      }
      {
        name: 'groupHelp'
        data: [
          {
            name: 'cmdHelp'
            toggle: false
            title: "Afficher l'aide Markdown"
            icon: "fa fa-question-circle"
            callback: (e) ->
              document.querySelector('.markdown-help')?.classList.toggle('visible')
          }
        ]
      }
    ]
    footer: (e) ->
      """
      <div class="progress" style="display: none; height: 10px;">
        <div class="progress-bar progress-bar-success"></div>
      </div>
      """
  })

  $md_editor = $(el).parent('.md-editor')
